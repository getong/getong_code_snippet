Erlang/OTP
在各个版本中引入了很多新特性，导致某些函数在不同的版本中不可用，因此编写版本兼容的代码很有必要。

* 1. 使用indef或者ifndef条件编译
:PROPERTIES:
:CUSTOM_ID: 使用indef或者ifndef条件编译
:END:
#+begin_example
%% 在riak_core
%% 在rebar.config 添加
{erl_opts, [
            {platform_define, "^R15", "old_hash"}
            ]
    }.

%% 在代码里面使用
-ifndef(old_hash).
sha(Bin) ->
    crypto:hash(sha, Bin).
-else.
sha(Bin) ->
    crypto:sha(Bin).
-endif.
#+end_example

推荐使用这种方式。像rand模块、不同版本的spec标记、crypto的
strong_rand_bytes/1函数等都可以使用这种方法

* 2. 使用try...catch...
:PROPERTIES:
:CUSTOM_ID: 使用trycatch
:END:
#+begin_example
%% 忘记在哪个项目使用了, 仿照下面的例子改写一个
ts() ->
    try
        %% 19.2
        erlang:system_time(microsecond)
    catch
        _:_ ->
            {MegaSeconds, Seconds, MicroSeconds} = erlang:now(),
            (MegaSeconds * 1000000 + Seconds) * 1000000 + MicroSeconds
    end.
#+end_example

* 3.使用erlang:function_exported/3函数检测是否有新的函数
:PROPERTIES:
:CUSTOM_ID: 使用erlangfunction_exported3函数检测是否有新的函数
:END:
#+begin_example
%% emqttd/src/emqttd_guid.erl
ts() ->
    case erlang:function_exported(erlang, system_time, 1) of
        true -> %% R18
            erlang:system_time(micro_seconds);
        false ->
            {MegaSeconds, Seconds, MicroSeconds} = os:timestamp(),
            (MegaSeconds * 1000000 + Seconds) * 1000000 + MicroSeconds
    end.
#+end_example

* 4.鸵鸟埋头大法, 屏蔽编译错误
:PROPERTIES:
:CUSTOM_ID: 鸵鸟埋头大法-屏蔽编译错误
:END:
#+begin_example
%% 在ejabberd
%% rebar.config
{erl_opts, [nowarn_deprecated_function]}.
#+end_example

不推荐使用。有些废弃的函数在模块中没有被删除掉，但不排除以后会。

** unsupported api
:PROPERTIES:
:CUSTOM_ID: unsupported-api
:END:
#+begin_src erlang
erlang:now()
random module
gen_fsm module
erlang:get_stacktrace() will be changed in the future
#+end_src

** erlang 21 deprecated get_stacktrace function
:PROPERTIES:
:CUSTOM_ID: erlang-21-deprecated-get_stacktrace-function
:END:
#+begin_src erlang
-ifdef(fun_stacktrace).
-define(WITH_STACKTRACE(T, R, S), T:R -> S = erlang:get_stacktrace(),).
-else.
-define(WITH_STACKTRACE(T, R, S), T:R:S ->).
-endif.
#+end_src

copy from rebar3 rebar.hrl

The rebar.config

#+begin_src erlang
{erl_opts, [
    {platform_define, "^(R|1|20)", fun_stacktrace}
]}.
#+end_src

** gen_fsm_compat
:PROPERTIES:
:CUSTOM_ID: gen_fsm_compat
:END:
The gen_fsm is deprecated by erlang team, but the gen_fsm_compat is
copied and maintained by Heinz N. Gies. The two modules are almost the
same. [[https://gitlab.com/Project-FiFo/gen_fsm_compat][gen_fsm_compat]]

** rebar_erl_vsn
:PROPERTIES:
:CUSTOM_ID: rebar_erl_vsn
:END:
[[https://github.com/project-fifo/rebar_erl_vsn][rebar_erl_vsn]] Use it
by adding this to rebar.config

#+begin_src erlang
{plugins, [rebar_erl_vsn]}.
{provider_hooks, [{pre, [{compile, erl_vsn}]}]}.
#+end_src

** stacktrace_compat
:PROPERTIES:
:CUSTOM_ID: stacktrace_compat
:END:
+[[https://github.com/g-andrade/stacktrace_compat/][stacktrace_compat]]+
use the code below:

#+begin_src erlang
-ifdef(OTP_RELEASE). %% this implies 21 or higher
-define(EXCEPTION(Class, Reason, Stacktrace), Class:Reason:Stacktrace).
-define(GET_STACK(Stacktrace), Stacktrace).
-else.
-define(EXCEPTION(Class, Reason, _), Class:Reason).
-define(GET_STACK(_), erlang:get_stacktrace()).
-endif.
#+end_src

** error_logger is not enabled by default and use logger since erlang 21, but it is still possible to use error_logger.
:PROPERTIES:
:CUSTOM_ID: error_logger-is-not-enabled-by-default-and-use-logger-since-erlang-21-but-it-is-still-possible-to-use-error_logger.
:END:
** erl_opts
:PROPERTIES:
:CUSTOM_ID: erl_opts
:END:
erlang compiler has the option =erl_opts= to include customize options.
There are two ways to change the erl_opts with the rebar. One is to
change it with the =rebar.config.script=

#+begin_src erlang
HaveDreyfus = code:lib_dir(dreyfus) /= {error, bad_name}.

if not HaveDreyfus -> CONFIG; true ->
    CurrOpts = case lists:keyfind(erl_opts, 1, CONFIG) of
        {erl_opts, Opts} -> Opts;
        false -> []
    end,
    NewOpts = [{d, 'HAVE_DREYFUS'} | CurrOpts],
    lists:keystore(erl_opts, 1, CONFIG, {erl_opts, NewOpts})
end.
#+end_src

copy from couchdb/src/mango/rebar.config.script

The other way is to use with rebar3 plugin:

#+begin_src erlang
-spec do(rebar_state:t()) -> {ok, rebar_state:t()} | {error, string()}.
do(State) ->
    Vsns = add_events(enumerate(version())),
    AppInfo = rebar_state:current_app(State),
    ErlOpts = rebar_app_info:get(AppInfo, erl_opts, []),
    AppInfo1 = rebar_app_info:set(AppInfo, erl_opts, Vsns ++ ErlOpts),
    %%io:format("vsns: ~p~n", [Vsns]),
    State1 = rebar_state:current_app(State, AppInfo1),
    {ok, State1}.
#+end_src

copy from rebar_erl_vsn

They get the local =erl_opts= options, and append the customize options
to it.
